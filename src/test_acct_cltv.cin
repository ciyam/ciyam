; Copyright (c) 2016 CIYAM Developers
;
; Distributed under the MIT/X11 software license, please refer to the file license.txt
; in the root project directory or http://www.opensource.org/licenses/mit-license.php.
;
; CIYAM Bitcoin test script for ACCT using either a CLTV or secret reveal redemption tx
; -------------------------------------------------------------------------------------
;
; NOTE: In order to utilise the following script you will first need to install Bitcoin
; into the directory where CIYAM has been compiled or else set the TBITCOIN environment
; variable accordingly (check the 'tbitcoin' script to work out how to do this and when
; doing this don't forget the '-regtest' option). Check that there is no 'regtest' data
; before starting 'bitcoind' and then running this script.
;
;USE_SECRET_REVEAL=
;USE_UNIX_TIMESTAMP=
;
;USE_SECRET_REVEAL=
;USE_UNIX_TIMESTAMP=1
;
;SECRET=1234567890
;USE_SECRET_REVEAL=1
;
@ifndef $USE_SECRET_REVEAL
#Redeeming using CLTV refund...
@ifndef $USE_UNIX_TIMESTAMP
#(via block number)
@else
#(via unix timestamp)
@endif
@else
@ifndef $SECRET
SECRET=1234567890
@endif
#Redeeming using secret reveal...
@endif
~tbitcoin generate 101
;
; Define an address and matching WIF private key for testing.
;
ADDR=mp7zjTrEszZezRBM1cUmTvaK2zf3MuDAHt
WKEY=cPR4DC5NXVkR37wEbCuUVuvuFxSozsMr8SHYteB2iqG8yV3uHdFR
;
; To simplify testing just uses the same address for redemption
;
crypto_addr_hash $ADDR
PKHS=$OUTPUT
PKHR=$OUTPUT
;
; Send some funds to the address of the newly imported private key.
;
~tbitcoin importprivkey $WKEY
TXID=@~tbitcoin sendtoaddress $ADDR 50
;
; Setup an ACCT P2SH script and then get its address and script hash.
;
SCTH=@sha256:$SECRET
@ifndef $USE_UNIX_TIMESTAMP
BLKN=7f
SCRIPT=76a820%SCTH%87637576a914%PKHS%88ac6701%BLKN%b17576a914%PKHR%88ac68
@else
TIME=@unix+5
HTME=@hexlit:$TIME
LTME=@substr:0,8:$HTME
SCRIPT=76a820%SCTH%87637576a914%PKHS%88ac6704%LTME%b17576a914%PKHR%88ac68
@endif
;
crypto_p2sh_addr TBTC $SCRIPT
P2SH_ADDR=$OUTPUT
crypto_addr_hash $P2SH_ADDR
P2SH_HASH=$OUTPUT
;
; Now send funds to the P2SH address.
;
~tbitcoin generate 3
TXID=@~tbitcoin sendtoaddress $P2SH_ADDR 50
OLD_TX=@~tbitcoin getrawtransaction $TXID
~tbitcoin decoderawtransaction $OLD_TX
;
; (check that the VOUT matches or change the next line)
; (also verify the P2SH scriptPubKey's script and hash)
;
VOUT=0
#$P2SH_HASH
~tbitcoin generate 3
~tbitcoin getblockcount
;
@ifndef $USE_UNIX_TIMESTAMP
LOCK=127
@else
LOCK=$TIME
@endif
@ifndef $USE_SECRET_REVEAL
crypto_p2sh_redeem $TXID $VOUT $SCRIPT $ADDR 50.0 $WKEY $LOCK
@else
HEX_SCRT=@aschex:$SECRET
crypto_p2sh_redeem $TXID $VOUT $SCRIPT $ADDR 50.0 $WKEY -e=$HEX_SCRT
@endif
RAW_TX=$OUTPUT
~tbitcoin decoderawtransaction $RAW_TX
~tbitcoin signrawtransaction $RAW_TX
@ifndef $USE_SECRET_REVEAL
@ifdef $USE_UNIX_TIMESTAMP
;
; Wait for five seconds to then redeem the funds from the P2SH UTXO.
;
; NOTE: When running with testnet/regtest it seems that when using a
; unix timestamp (instead of a block number) then the tx is accepted
; before the actual time so this delay is just to ensure that the tx
; will appear confirmed after the final generate).
;
wait 5000
@else
;
; Note this attempt will fail due to being non-final (i.e. locktime).
;
~tbitcoin sendrawtransaction $RAW_TX
;
; Increase the block height then redeem the funds from the P2SH UTXO.
;
~tbitcoin generate 20
@endif
@endif
~tbitcoin getblockcount
TXID=@~tbitcoin sendrawtransaction $RAW_TX
NEW_TX=@~tbitcoin getrawtransaction $TXID
~tbitcoin decoderawtransaction $NEW_TX
~tbitcoin generate 1
