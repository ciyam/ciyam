; Copyright (c) 2019-2022 CIYAM Developers
;
; Distributed under the MIT/X11 software license, please refer to the file license.txt
; in the root project directory or http://www.opensource.org/licenses/mit-license.php.
;
@ifndef $1
#NOTE: The script requires a starting directory point to import from the branch below.
@else
TAG_PREFIX=$2
SUB_DIRECTORY=$3
FILES_EXPRESSION=@repstr:;/,:$4
PATHS_EXPRESSION=@repstr:;/,:$5
;
IS_CURRENT=
@ifeq "$1" "."
@ifeq "$1" "$3"
IS_CURRENT=1
@endif
@endif
;
FILES_EXTRA=
@ifndef $FILES_EXPRESSION
FILES_EXTRA=::10.8G
@else
FILES_EXTRA=:$FILES_EXPRESSION:10.8G
@endif
;
PATHS_EXTRA=
@ifdef $PATHS_EXPRESSION
PATHS_EXTRA=:$PATHS_EXPRESSION
@endif
;
@ifndef $MAX_FILE_SIZE
MAX_FILE_SIZE=1M
@endif
;
SUB_DIR_IS_LEAF=
;
START=@fullpath:$1
@ifndef $ERROR
.session_variable @uuid
UUID=$OUTPUT
;
@ifeq "$SUB_DIRECTORY" ":"
PATHS=.
SUB_DIRECTORY=
SUB_DIR_IS_LEAF=1
@else
@ifndef $SUB_DIRECTORY
PATHS=@paths:$START$PATHS_EXTRA
@else
@ifndef $IS_CURRENT
PATHS=@paths:$START$PATHS_EXTRA/$SUB_DIRECTORY
@endif
@ifndef $PATHS
.session_variable @deque ""
DIRS=@repstr:\//\n:$SUB_DIRECTORY
@ifneq $SUB_DIRECTORY "$DIRS"
SUB_DIR_IS_LEAF=1
TAG_PREFIX=@substr:0,-1:$TAG_PREFIX
.session_variable @deque "~$DIRS"
.session_variable @deque back
PATHS=$OUTPUT
.session_variable @deque pop_back
.session_variable @deque
SUB_DIRECTORY=@repstr:\n/\/:$OUTPUT
@else
SUB_DIR_IS_LEAF=1
PATHS=$SUB_DIRECTORY
SUB_DIRECTORY=
@endif
@endif
@endif
@endif
.session_variable @set ""
.session_variable @set "~$PATHS"
;
@:PATH_LOOP
@label NEXT_PATH
.session_variable @deque ""
.session_variable @deque @set
.session_variable @set ""
;
.session_variable @deque front
NEXT_PATH=$OUTPUT
;
@ifndef $NEXT_PATH
@label EXIT_PATH
@endif
;
@skip
@:NEXT_PATH
IS_FIRST=
HAD_ANY_FILES=
.session_variable @deque pop_front
.session_variable @set @deque
;
@ifndef $SUB_DIRECTORY
FILES=@files:$START/$NEXT_PATH$FILES_EXTRA
@else
FILES=@files:$START/$SUB_DIRECTORY/$NEXT_PATH$FILES_EXTRA
@endif
.session_variable @deque ""
.session_variable @deque "~$FILES"
;
@:FILE_LOOP
@label NEXT_FILE
;
.session_variable @deque front
NEXT_FILE=$OUTPUT
;
@ifndef $NEXT_FILE
@label EXIT_FILE
@endif
;
@skip
@:NEXT_FILE
;
@ifdef $HAD_ANY_FILES
IS_FIRST=
@else
IS_FIRST=1
@endif
HAD_ANY_FILES=1
.session_variable @deque pop_front
;
@ifndef $SUB_DIRECTORY
@ifeq "." "$NEXT_PATH"
@ifdef $IS_CURRENT
FILE_NAME=$NEXT_FILE
@else
FILE_NAME=$1/$NEXT_FILE
@endif
@else
FILE_NAME=$1/$NEXT_PATH/$NEXT_FILE
@endif
@else
FILE_NAME=$1/$SUB_DIRECTORY/$NEXT_PATH/$NEXT_FILE
@endif
;
; NOTE: If file name has an offset and chunk size
; suffix then remove the chunk size part.
FILE_NAME=@repstr:\:/\n:$FILE_NAME
NAME_PREFIX=%FILE_NAME:1%
NAME_SUFFIX=%FILE_NAME:2%
@ifneq "$NAME_SUFFIX" ""
NAME_SUFFIX=@repstr:+/\n:$NAME_SUFFIX
@ifneq "$NAME_SUFFIX" ""
NAME_SUFFIX=:%NAME_SUFFIX:1%
@endif
@endif
FILE_NAME=$NAME_PREFIX$NAME_SUFFIX
;
; NOTE: Each file is split into MAX_FILE_SIZE chunks and
; the UUID variable is used to tag the "list" file which
; will be automatically created by "ciyam_client".
@ifndef $SUB_DIRECTORY
.file_put $MAX_FILE_SIZE*$1//$NEXT_PATH/$NEXT_FILE $START/?$UUID
@else
.file_put $MAX_FILE_SIZE*$1//$SUB_DIRECTORY/$NEXT_PATH/$NEXT_FILE $START/?$UUID
@endif
;
@ifdef $ERROR
ERROR=
@ifdef $IS_FIRST
HAD_ANY_FILES=
@endif
@else
; NOTE: Get the file hash of the tagged "list" of chunks
; and append it to a "list" that will be effectively the
; list of files that are in a specific directory.
.file_hash $UUID
HASH=$OUTPUT
;
; NOTE: If file name has an offset and chunk size
; suffix then remove the chunk size part.
NEXT_FILE=@repstr:\:/\n:$NEXT_FILE
NAME_PREFIX=%NEXT_FILE:1%
NAME_SUFFIX=%NEXT_FILE:2%
@ifneq "$NAME_SUFFIX" ""
NAME_SUFFIX=@repstr:+/\n:$NAME_SUFFIX
@ifneq "$NAME_SUFFIX" ""
NAME_SUFFIX=:%NAME_SUFFIX:1%
@endif
@endif
NEXT_FILE=$NAME_PREFIX$NAME_SUFFIX
;
>>~$UUID
@ifndef $SUB_DIRECTORY
@ifeq "." "$NEXT_PATH"
#$HASH $NEXT_FILE
@else
#$HASH $NEXT_PATH/$NEXT_FILE
@endif
@else
#$HASH $SUB_DIRECTORY/$NEXT_PATH/$NEXT_FILE
@endif
>>
@endif
;
!!@:FILE_LOOP
;
@:EXIT_FILE
;
@ifdef $HAD_ANY_FILES
@ifdef $SUB_DIR_IS_LEAF
TAG_NAME=
@else
; NOTE: Change '/' to '.' for the tag.
TAG_NAME=@repstr:\//.:$NEXT_PATH
@endif
;
.file_put ?~$UUID $TAG_PREFIX$TAG_NAME
~rm ~$UUID
.file_tag -remove $UUID
@endif
;
!!@:PATH_LOOP
;
@:EXIT_PATH
@endif
@endif
