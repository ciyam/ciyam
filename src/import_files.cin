; Copyright (c) 2019-2021 CIYAM Developers
;
; Distributed under the MIT/X11 software license, please refer to the file license.txt
; in the root project directory or http://www.opensource.org/licenses/mit-license.php.
;
@ifndef $1
#NOTE: The script requires a starting directory point to import from the branch below.
@else
TAG_PREFIX=$2
SUB_DIRECTORY=$3
FILES_EXPRESSION=$4
;
FILES_EXTRA=
@ifdef $FILES_EXPRESSION
FILES_EXTRA=:$FILES_EXPRESSION
@endif
;
@ifndef $MAX_FILE_SIZE
MAX_FILE_SIZE=1M
@endif
;
SUB_DIR_IS_LEAF=
;
START=@fullpath:$1
@ifndef $ERROR
.session_variable @uuid
UUID=$OUTPUT
;
@ifndef $SUB_DIRECTORY
PATHS=@paths:$START
@else
PATHS=@paths:$START/$SUB_DIRECTORY
@ifndef $PATHS
.session_variable @deque ""
DIRS=@repstr:\//\n:$SUB_DIRECTORY
@ifneq $SUB_DIRECTORY "$DIRS"
SUB_DIR_IS_LEAF=1
TAG_PREFIX=@substr:0,-1:$TAG_PREFIX
.session_variable @deque "~$DIRS"
.session_variable @deque back
PATHS=$OUTPUT
.session_variable @deque pop_back
.session_variable @deque
SUB_DIRECTORY=@repstr:\n/\/:$OUTPUT
@else
SUB_DIR_IS_LEAF=1
PATHS=$SUB_DIRECTORY
SUB_DIRECTORY=
@endif
@endif
@endif
.session_variable @set ""
.session_variable @set "~$PATHS"
;
@:PATH_LOOP
@label NEXT_PATH
.session_variable @deque ""
.session_variable @deque @set
.session_variable @set ""
;
.session_variable @deque front
NEXT_PATH=$OUTPUT
;
@ifndef $NEXT_PATH
@label EXIT_PATH
@endif
;
@skip
@:NEXT_PATH
IS_FIRST=
HAD_ANY_FILES=
.session_variable @deque pop_front
.session_variable @set @deque
;
@ifndef $SUB_DIRECTORY
FILES=@files:$START/$NEXT_PATH$FILES_EXTRA
@else
FILES=@files:$START/$SUB_DIRECTORY/$NEXT_PATH$FILES_EXTRA
@endif
.session_variable @deque ""
.session_variable @deque "~$FILES"
;
@:FILE_LOOP
@label NEXT_FILE
;
.session_variable @deque front
NEXT_FILE=$OUTPUT
;
@ifndef $NEXT_FILE
@label EXIT_FILE
@endif
;
@skip
@:NEXT_FILE
@ifndef $HAD_ANY_FILES
@ifndef $SUB_DIRECTORY
#$1/$NEXT_PATH
@else
#$1/$SUB_DIRECTORY/$NEXT_PATH
@endif
@endif
;
@ifdef $HAD_ANY_FILES
IS_FIRST=
@else
IS_FIRST=1
@endif
HAD_ANY_FILES=1
.session_variable @deque pop_front
;
@ifndef $SUB_DIRECTORY
#$1/$NEXT_PATH/$NEXT_FILE
@else
#$1/$SUB_DIRECTORY/$NEXT_PATH/$NEXT_FILE
@endif
;
; NOTE: Each file is split into MAX_FILE_SIZE chunks and
; the UUID variable is used to tag the "list" file which
; will be automatically created by "ciyam_client".
@ifndef $SUB_DIRECTORY
.file_put $MAX_FILE_SIZE*$1//$NEXT_PATH/$NEXT_FILE $START/?$UUID
@else
.file_put $MAX_FILE_SIZE*$1//$SUB_DIRECTORY/$NEXT_PATH/$NEXT_FILE $START/?$UUID
@endif
;
@ifdef $ERROR
ERROR=
@ifdef $IS_FIRST
HAD_ANY_FILES=
@endif
@else
; NOTE: Get the file hash of the tagged "list" of chunks
; and append it to a "list" that will be effectively the
; list of files that are in a specific directory.
.file_hash $UUID
HASH=$OUTPUT
>>~$UUID
@ifndef $SUB_DIRECTORY
#$HASH $NEXT_PATH/$NEXT_FILE
@else
#$HASH $SUB_DIRECTORY/$NEXT_PATH/$NEXT_FILE
@endif
>>
@endif
;
!!@:FILE_LOOP
;
@:EXIT_FILE
;
@ifdef $HAD_ANY_FILES
@ifdef $SUB_DIR_IS_LEAF
TAG_NAME=
@else
; NOTE: Change '/' to '.' for the tag.
TAG_NAME=@repstr:\//.:$NEXT_PATH
@endif
;
.file_put ?~$UUID $TAG_PREFIX$TAG_NAME
~rm ~$UUID
.file_tag -remove $UUID
@endif
;
!!@:PATH_LOOP
;
@:EXIT_PATH
@endif
@endif
