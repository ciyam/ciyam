; Copyright (c) 2026 CIYAM Developers
;
; Distributed under the MIT/X11 software license, please refer to the file license.txt
; in the root project directory or http://www.opensource.org/licenses/mit-license.php.
;
<ciyam.fissile
;
NUM_ARGS=0
@ifdef $1
NUM_ARGS=@$NUM_ARGS+1
@endif
;
@ifneq "$NUM_ARGS" "1"
#NOTE: This script requires a core peer identity.
@else
ERROR=
;
IDENTITY=$1
;
OKAY=
FOUND=
;
REVERSED=*!reverse?$IDENTITY
;
<check_system_identity.cin
;
@ifdef $ERROR
#Error: $ERROR
@else
PEER_IDENTITY=$IDENTITY
;
.system_variable @files_area_path
FILES_AREA_DIR=$OUTPUT
;
.peerchain_info -list
PEERS=$OUTPUT
;
CHECK_PEERS=@repstr:$IDENTITY/:$PEERS
;
@ifeq "$PEERS" "$CHECK_PEERS"
ERROR=Unknown or invalid peer identity $IDENTITY.
@else
.peerchain_info $IDENTITY peer_type
PEER_TYPE=$OUTPUT
;
.peerchain_info $IDENTITY extra_value
EXTRA_VALUE=$OUTPUT
;
@ifeq "$PEER_TYPE" "3"
PEER_IDENTITY=$REVERSED
@endif
@endif
;
@ifdef $ERROR
#Error: $ERROR
@else
;
.file_tags bc.$PEER_IDENTITY.zenith*
@ifdef $OUTPUT
.file_tags bc.$PEER_IDENTITY.locked*
@ifndef $OUTPUT
OKAY=1
##Identity $IDENTITY appears to have already been verified.
@endif
@endif
;
@ifndef $OKAY
;
.system_variable %%$PEER_IDENTITY
@ifndef $OUTPUT
.system_variable @queue_peers $IDENTITY
.wait 2000
@endif
.system_variable #$PEER_IDENTITY
@ifndef $OUTPUT
.system_variable %%$PEER_IDENTITY
@ifdef $OUTPUT
OUTPUT=
@else
.wait 2000
.system_variable #$PEER_IDENTITY
@endif
@endif
;
@ifdef $OUTPUT
ERROR=$OUTPUT
#Error: $ERROR
@else
.session_list $PEER_IDENTITY
@ifdef $OUTPUT
FOUND=1
@else
ERROR=No session was found for $IDENTITY.
#Error: $ERROR
@endif
@endif
;
@ifdef $FOUND
;
I=0
@label NEXT
;
@:LOOP
;
.file_tags bc.$PEER_IDENTITY.locked*
@ifdef $OUTPUT
OKAY=1
.wait 1000
@label END_LOOP
@else
@ifeq "$I" "25"
@label END_LOOP
ERROR=Timed out attempting to verify (check before retrying).
#Error: $ERROR
@endif
@endif
;
@skip
;
@:NEXT
;
##.
I=@$I+1
.wait 2000
;
!!@:LOOP
;
@:END_LOOP
;
@ifeq "$OKAY" "1"
;
.session_list $PEER_IDENTITY
@ifdef $OUTPUT
.wait 1000
@endif
;
.session_list $PEER_IDENTITY
@ifdef $OUTPUT
.wait 2000
@endif
;
@ifneq "$PEER_TYPE" "3"
.system_variable @blockchain_backup_%EXTRA_VALUE%_identity
.file_hash bc.$OUTPUT.master
LOCAL_BACKUP_HASH=$OUTPUT
LOCAL_CHECKSUM_VALS=$LOCAL_BACKUP_HASH
;
.file_hash bc.$IDENTITY.0.blk
REMOTE_BACKUP_HASH=$OUTPUT
REMOTE_CHECKSUM_VALS=$REMOTE_BACKUP_HASH
@endif
;
@ifneq "$PEER_TYPE" "2"
.system_variable @blockchain_shared_%EXTRA_VALUE%_identity
.file_hash bc.$OUTPUT.master
LOCAL_SHARED_HASH=$OUTPUT
LOCAL_CHECKSUM_VALS=$LOCAL_SHARED_HASH
;
; NOTE: Allows for a small lag
; if using paired blockchains.
.file_tags bc.$REVERSED.0.blk*
@ifndef $OUTPUT
.wait 2000
@endif
.file_hash bc.$REVERSED.0.blk
REMOTE_SHARED_HASH=$OUTPUT
REMOTE_CHECKSUM_VALS=$REMOTE_SHARED_HASH
@endif
;
@ifeq "$PEER_TYPE" "0"
LOCAL_CHECKSUM_VALS=$LOCAL_BACKUP_HASH,$LOCAL_SHARED_HASH
REMOTE_CHECKSUM_VALS=$REMOTE_BACKUP_HASH,$REMOTE_SHARED_HASH
@endif
;
.crypto_checksum $LOCAL_CHECKSUM_VALS
#Local Checksum: $OUTPUT
.crypto_checksum $REMOTE_CHECKSUM_VALS
#Remote Checksum: $OUTPUT
;
&Checksum Correct? [CORRECT:Yes=1|No=!*** NO ***]
;
@ifdef $CORRECT
;
.system_variable @storage
STORAGE=$OUTPUT
@ifeq "$STORAGE" "Meta"
STORAGE=
@endif
;
@ifeq "$STORAGE" ""
.file_tag -remove bc.$PEER_IDENTITY.locked
&Reconnect To Peer Now? [CONNECT:Yes==1|No=]
@ifdef $CONNECT
.system_variable @queue_peers $IDENTITY
@endif
;
@else
;
@retain *
<$STORAGE.create_core_peer.vars.lst
;
.storage_init $STORAGE
.pe admin @now $STORAGE $CLASS_NAME $IDENTITY $PROCEDURE
.storage_term
@endif
;
@endif
;
@else
;
@ifndef $ERROR
.system_variable @queue_peers
@ifdef $OUTPUT
ERROR=It appears that peer handling is not currently active.
#Error: $ERROR
@else
ERROR=$IDENTITY does not appear to be a valid identity.
#Error: $ERROR
@endif
@endif
@endif
@endif
;
@endif
@endif
;
@endif
@endif
