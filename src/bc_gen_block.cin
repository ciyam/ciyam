; Copyright (c) 2021-2022 CIYAM Developers
;
; Distributed under the MIT/X11 software license, please refer to the file license.txt
; in the root project directory or http://www.opensource.org/licenses/mit-license.php.
;
ERROR=
@ifeq "$1" ""
ERROR=1
@endif
@ifeq "$2" ""
ERROR=1
@endif
;
@ifeq "$ERROR" "1"
#Usage: bc_gen_block.cin <ident> <height>
@else
;
IDENT=$1
HEIGHT=$2
;
@ifneq "$IDENT" "da667b8c4"
UTIME=@unix:
@else
HRS_01=3600
HRS_24=@$HRS_01*24
;
HRS_HEIGHT=@$HRS_24*$HEIGHT
;
UTIME=@1635400000+$HRS_HEIGHT
@endif
;
.session_variable @uuid
UUID=$OUTPUT
;
@ifeq "$HEIGHT" "0"
#Error: Height must be greater than zero.
@else
LAST=@$HEIGHT-1
;
.file_hash bc.$IDENT.$LAST.blk
@ifndef $ERROR
;
LAST_HASH=$OUTPUT
.encode $LAST_HASH
LAST_HASH_ENC=$OUTPUT
;
.file_hash bc.$IDENT.p$LAST.pub
@ifndef $ERROR
;
.file_hash bc.$IDENT.p$HEIGHT.pub
@ifndef $ERROR
.encode $OUTPUT
PUB_KEY_HASH_ENC=$OUTPUT
;
.file_hash bc.$IDENT.$HEIGHT.dat
@ifndef $ERROR
;
DATA_HASH=$OUTPUT
.encode $DATA_HASH
DATA_HASH_ENC=$OUTPUT
;
.file_crypt bc.$IDENT.p$LAST.key @sid
.file_get bc.$IDENT.p$LAST.key ~$UUID.key
.file_crypt bc.$IDENT.p$LAST.key @sid
;
.file_get bc.$IDENT.p$LAST.pub ~$UUID.pub
;
.crypto_lamport -sign ~$UUID:$DATA_HASH
.file_put ~$UUID.sig bc.$IDENT.$LAST.sig
.file_hash bc.$IDENT.$LAST.sig
SIG_HASH=$OUTPUT
.encode $SIG_HASH
SIG_HASH_ENC=$OUTPUT
;
.file_raw -core blob blk:h=$HEIGHT,i=$IDENT\n\
l:$LAST_HASH_ENC\n\
p:$PUB_KEY_HASH_ENC\n\
s:$SIG_HASH_ENC\n\
u:$UTIME bc.$IDENT.$HEIGHT.blk
;
.file_tag $OUTPUT bc.$IDENT.zenith
;
.file_tag -unlink bc.$IDENT.p$LAST.key
;
.system_variable @os
@ifeq "$OUTPUT" "Linux"
~shred ~$UUID.key
@endif
~rm ~$UUID.key ~$UUID.pub ~$UUID.sig
@endif
@endif
@endif
@endif
@endif
@endif
