; Copyright (c) 2024-2025 CIYAM Developers
;
; Distributed under the MIT/X11 software license, please refer to the file license.txt
; in the root project directory or http://www.opensource.org/licenses/mit-license.php.
;
@ifndef $1
#Usage: prepare_backup.cin <storage>
@else
;
STORAGE=$1
;
APP_DIR=@lower:$STORAGE
;
.system_variable @prepare_system 1 ""
@ifeq "$OUTPUT" "1"
.system_variable @prepare_in_progress 1
~echo "(backup preparation is now commencing)" > Backup.txt
;
.wait 5000
;
@ifdef $WEBDIR
~touch $WEBDIR/$APP_DIR/.prepare.backup
@endif
;
.system_variable @disallow_connections 1
;
.session_kill *
.session_variable @single_string_response 1
;
CHECK=0
NUM_TRIES=0
MAX_TRIES=60
;
@label NEXT
@:LOOP
;
.wait 1000
NUM_TRIES=@$NUM_TRIES+1
;
.session_list -min
SECOND_LINE=%OUTPUT:2%
;
@ifndef $SECOND_LINE
@label END_LOOP
@endif
;
CHECK=@$NUM_TRIES>$MAX_TRIES
@ifeq "$CHECK" "1"
@label END_LOOP
@endif
;
@skip
;
@:NEXT
;
!!@:LOOP
;
@:END_LOOP
;
@ifeq "$CHECK" "1"
;
.system_variable @disallow_connections ""
;
ERROR=Failed due to active sessions not being automatically terminated.
~echo "*** ERROR: BACKUP FAILED DUE TO ACTIVE SESSIONS NOT AUTOMATICALLY TERMINATED ***" > Backup.txt
@else
;
REMOVE_OBSOLETE=@files::.remove_obsolete
;
@ifdef $REMOVE_OBSOLETE
LINES=@file:.remove_obsolete
;
@:REPO_LOOP
;
@label NEXT
;
NEXT_IDENTITY=%LINES:1%
;
@ifeq "$NEXT_IDENTITY" ""
@label END_REPO_LOOP
@endif
;
@skip
;
@:NEXT
;
.file_repo_entries -remove_obsolete $NEXT_IDENTITY
;
LINES=$LINES:-1%
;
!!@:REPO_LOOP
;
@:END_REPO_LOOP
;
~rm -f .remove_obsolete && touch .remove_obsolete
;
@endif
;
.system_variable @disallow_connections ""
;
@ifdef $WEBDIR
CIYAM_EXEC_SYSTEM_APPEND=$WEBDIR/$APP_DIR/backup.log
~rm -f Backup.txt
~./log_head.sh $CIYAM_EXEC_SYSTEM_APPEND
~./ciyam_backup $STORAGE $APP_DIR >> $CIYAM_EXEC_SYSTEM_APPEND
~./log_tail.sh $CIYAM_EXEC_SYSTEM_APPEND
;
.system_variable @backup_files
BACKUP_FILES_PATH=$OUTPUT
;
@ifdef $BACKUP_FILES_PATH
~mkdir -p $BACKUP_FILES_PATH/.ciyam
~mv $STORAGE.backup*.bun.gz $BACKUP_FILES_PATH/.ciyam
<create_peer_info_file_list.cin "~prepare_backup.files"
<append_hub_and_extras_list.cin "~prepare_backup.files"
~tar -cf $BACKUP_FILES_PATH/.ciyam/System_Files_And_Archives.tar --files-from="~prepare_backup.files"
~rm -f .prepare_backup_needed "~prepare_backup.files"
#Prepared application server files, archives and databases.
.system_variable @backup_needed 1
@endif
@else
~./ciyam_backup $STORAGE $APP_DIR
@endif
@endif
;
.system_variable @preparing_backup ""
.system_variable @prepare_in_progress ""
;
@ifdef $WEBDIR
~rm -f $WEBDIR/$APP_DIR/.prepare.backup
@endif
;
.system_variable @prepare_system ""
@endif
;
@endif
