; Copyright (c) 2022-2025 CIYAM Developers
;
; Distributed under the MIT/X11 software license, please refer to the file license.txt
; in the root project directory or http://www.opensource.org/licenses/mit-license.php.
;
@ifndef $1
#NOTE: This script requires a blockchain identity (else "-hub", "-backup" or "-shared").
@else
ERROR=
;
IDENT_VAL=$1
SEED_VALUE=$2
TREE_TAG_VAL=$3
;
FIRST=1
BACKUP=
SHARED=
PEER_HUB=
;
HAS_NO_OPL=
;
.session_variable @deque commence
;
@label NEXT
;
@ifdef $TREE_TAG_VAL
.file_hash $TREE_TAG_VAL
@ifdef $ERROR
@label END_LOOP
@endif
@endif
;
@skip
;
@:NEXT
@ifeq "$SEED_VALUE" "dummy"
SEED_VALUE=
@endif
;
@ifeq "$IDENT_VAL" "-backup"
BACKUP=1
@endif
;
@ifdef $CIYAM_SECONDS
.session_variable @progress_seconds $CIYAM_SECONDS
@endif
;
@ifdef $BACKUP
.system_variable @blockchain_backup_identity
IDENT_VAL=$OUTPUT
.system_variable @blockchain_backup_?_identity
@ifdef $OUTPUT
.session_variable @deque "~$OUTPUT"
@endif
@endif
;
@ifeq "$1" "-shared"
.system_variable @blockchain_shared_identity
IDENT_VAL=$OUTPUT
SHARED=1
.system_variable @blockchain_shared_?_identity
@ifdef $OUTPUT
.session_variable @deque "~$OUTPUT"
@endif
@endif
;
@ifeq "$1" "-hub"
.system_variable @blockchain_peer_hub_identity
IDENT_VAL=$OUTPUT
PEER_HUB=1
.system_variable @blockchain_peer_hub_?_identity
@ifdef $OUTPUT
.session_variable @deque "~$OUTPUT"
@endif
@endif
;
@ifeq "$SEED_VALUE" ""
.system_variable @blockchain_data_key_$IDENT_VAL
@ifndef $OUTPUT
SEED_VALUE=@sid
@else
; NOTE: Double escape is required as this variable
; is being passed to other scripts as an argument.
SEED_VALUE=\\@sid:$OUTPUT
@endif
@endif
;
UTIME=@unix:
;
@:LOOP
;
@ifneq "$IDENT_VAL" ""
.file_core -header bc.$IDENT_VAL.zenith h
@ifndef $ERROR
BLK_HEIGHT=$OUTPUT
NEW_HEIGHT=@$BLK_HEIGHT+1
;
@ifdef $PEER_HUB
<bc_gen_put_list.cin $IDENT_VAL $NEW_HEIGHT
.file_tags bc.$IDENT_VAL.$NEW_HEIGHT.opl*
@ifndef $OUTPUT
HAS_NO_OPL=1
@endif
@endif
;
@ifndef $HAS_NO_OPL
<bc_gen_keys.cin $SEED_VALUE $IDENT_VAL $BLK_HEIGHT
<bc_gen_keys.cin $SEED_VALUE $IDENT_VAL $NEW_HEIGHT
;
; NOTE: The .key file for the new height
; is removed (as only the current height
; key is used for the block signature).
.file_kill -q bc.$IDENT_VAL.$NEW_HEIGHT.key
;
<bc_gen_block.cin $IDENT_VAL $NEW_HEIGHT $TREE_TAG_VAL
;
@ifndef $ERROR
; FUTURE: This message should be handled as a server string.
.system_variable %$IDENT_VAL "Currently at height $NEW_HEIGHT"
@endif
;
@ifdef $FIRST
FIRST=
@ifndef $SHARED
@ifndef $PEER_HUB
.file_repo_entries -remove_obsolete $IDENT_VAL
@endif
@endif
@endif
;
@endif
@endif
@endif
;
@label NEXT
.session_variable @deque front
@ifdef $OUTPUT
LINES=@repstr: /\n:$OUTPUT
IDENT_VAL=%LINES:2%
@else
@label END_LOOP
@endif
@skip
;
@:NEXT
.session_variable @deque pop_front
!!@:LOOP
;
@:END_LOOP
;
.session_variable @deque complete
;
@endif
